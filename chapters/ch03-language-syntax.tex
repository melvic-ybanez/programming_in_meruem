In this chapter, we are going to focus the discussion on the structure of Meruem code. We are going to talk about it's odd-looking syntax, and why it is something you should try to embrace rather than avoid. Let us start the discussion with the main (and probably the only) syntax component of a Meruem program: \keyword{S-expressions}

\section{S-expressions}
The syntax of Meruem code is composed only of s-expressions. In fact the syntax for the whole program itself is an s-expression. So the key to mastering the syntax of Meruem is to learn how s-expressions work. The good news is it's actually pretty easy to learn s-expressions. So what is an s-expression?

An \ikeyword{s-expression} (or \code{sexprs})is a recursive tree-like data structure whose \keyword{leaf sub-nodes} are \keyword{atoms} and the non-\keyword{leaf sub-nodes} are themselves s-expressions. 
(A \keyword{leaf} node is a node that doesn't have any children.) In other words, an s-expression can be either an atom (the smallest unit of expression in Meruem), or a composition of other s-expressions (like I said, it's recursive). 

I'm sure you're already familiar with \code{atom}s, but just to refresh your memory let's see some of them again:

\begin{REPL}
meruem> 10            
10
meruem> 30@l
30
meruem> 50.79@f
50.79
meruem> 70@d
70.0
meruem> \c
\c
meruem> cons
<function>
meruem>   
\end{REPL}

Again, atoms are the first type of s-expression. The second one has the following structure: 

\begin{QuasiLang}
(elem1 elem2 elem3)
\end{QuasiLang}

where \code{elem1}, \code{elem2}, and \code{elem3} are all s-expressions. Does it look familiar? That's right! It looks like a \code{list}. This is why in the previous chapter, I mentioned that the Meruem program itself can be considered as a list. The following expressions are s-expressions that are not atoms:

\begin{REPL}
meruem> (list 1 2 3)
(1 2 3)
meruem> (list "the quick brown" (list \f \o \x))
(the quick brown (f o x))
meruem> (++ "I want" "cookies")
"I wantcookies"
meruem> 
\end{REPL}

It is worth noting that this type of s-expressions and lists are not always the same. A list is a data structure, while an s-expression refers to the syntax itself. For instance, consider the expression \code{(+ 1 2 4)}. If you run it in the REPL, you'll get \code{7} as a result. So, the expression \code{(+ 1 2 4)} is an s-expression but you wouldn't call it a list. It is a number, just like $1 + 3$ is a number expressed as the sum of $1$ and $3$. In other words, the type of the expression is the type of the value returned when we evaluate it. On the other hand, the expression \code{(list 1 2 3)} is an s-expression and a list, because the value it returns when evaluated, which is \code{(1 2 3)}, is a list. The difference between s-expression and list, however, does not invalidate the claim that Meruem programs are just lists. Internally, everything is still a list. Also, you can capture an entire expression (an s-expression) without evaluating it, effecting treating it as an ordinary list (see section~). For this reason, the two terms can be used almost interchangeably.

\section{Callable expressions}
Not all s-expressions (or lists) can be evaluated. For instance, if you are going to evaluate \code{(1 2 3)}, you'll get an error:

\begin{REPL}
meruem> (1 2 3)
An error has occurred. 1 can not be converted to a function.
Source: .home.melvic.meruem.meruem.prelude [1:2}]
(1 2 3)
 ^
\end{REPL}

So why can we evaluate \code{(list 1 2 3)} and not \code{(1 2 3)}? The error message from the last example made it sound like the interpreter was expecting for the first element of the list to be a function. And that's, indeed, what was happening. To be more accurate, the interpreter requires that the first element of a list should be a \keyword{callable}. A callable is a thing that can be applied to some parameters, like a function, operator, special commands, macros, and more. An expression whose first element is a callable is known as a \keyword{callable expression}. The syntax of a callable expression looks like this:

\begin{QuasiLang}
(callable-name param1 param2 param3 ...)
\end{QuasiLang}

\code{callable-name} refers to the name of the callable (like \code{list} or \code{++}). The \code{...} indicates that the number of parameters (or \keyword{params}) are not limited to only 3. Let's have some examples (you've already seen some of them):

\begin{REPL}
meruem> (list \c \d)
(c d)
meruem> (+ 6 7 (- 9 20))
2
meruem> (cons \m "eruem")
"meruem"
\end{REPL}

In Meruem, most of the callables are effectively functions. An operator like \code{+} is just a function who happened to have that operator-looking name. (In many languages, naming a function like this is not possible.). Macros are also just special functions, internally. Most of the special commands behave like functions as well. In fact, I rarely use the term "callable" in Meruem. I just refer to these things as "function"s. So does the interpreter (see the last error message). You'll learn more about functions in the later part of the book.

\section{Special Syntaxes}

There are some parts in a Meruem code that are not in a s-expressions form. The following subsections will focus on each of them.

\subsection{Comments}
A \ikeyword{comment} is a part of the code that the interpreter just ignores. Comments are useful when you want to write something in plain English without letting the interpreter throw some \keyword{parse errors}. Usually, you comment things for documentation purposes. Comments in Meruem are any lines of code that start with the \code{;} character. Any character following the \code{;} up to the end of the line will be ignored by the interpreter. The following example shows that commented parts of the code will not be included in the evaluation:

\begin{REPL}
meruem> (+ 56 7) ; this is a comment
63
meruem> "hello" ; world
"hello"
\end{REPL}

Becareful when writing comments though. If you insert it inside an expression, it might cause a parse error:

\begin{REPL}
meruem> (* 50 20)  ; this will run just fine
1000
meruem> (* 50 ; this will return an error 20)
An error has occurred. Parse Failure: `)' expected but end of source found
Source: .home.melvic.meruem.meruem.prelude [11:53}]
(defun truthy? (expr) (and (!= expr false) (!= expr nil)))

                                                    ^
\end{REPL}

To the eyes of the interpreter, the second example is just equivalent to \code{(* 50}, and that is not a valid s-expression. You can, however, do it like this:

\begin{Meruem}
(defun main (args)    ; This is the main function.
  ; The main functions is the entry point of a Meruem program.
  (println "I will still be printed"))
\end{Meruem}

The code shows that the elements in a list can be separated by multiple spaces, tabs, and newline characters, and not just by single spaces in between. Since this code spans multiple lines (not to mention it actually contains a \keyword{main} function, but more on this later), it is a good idea to save it as a source file using the Winter text editor (instead of running it directly in the REPL). Then run it via \code{java -jar meruem.jar filename} like you did in section~\sectionref{sec:helloworld}. I trust that from now on, you can tell which code samples are not to be run in a REPL. 

\subsection{Quotes}
As I've mentioned earlier, you can prevent an expression from being evaluated. To do so, you'll just have to add \code{'} at the beginning of the expression:

\begin{REPL}
meruem> '456
456
meruem> '(1 4 5)
(1 4 5)
meruem> '(list 3 5 6)
(list 3 5 6)
\end{REPL}

The \code{'} is known as the \keyword{quote} operator. Actually, the above expressions still got evaluated, but the resulting values are just the same as the corresponding inputs but without the quotes. This operator is one of the most important syntactic sugars (syntax that make things more pleasant to read) in Meruem, so expect to see more uses of it from now on. There is a more advanced form of quote, called \keyword{unquote}, but we are not going to discuss it in this chapter.

\subsection{List of Pairs}
It is not uncommon for an expression to be a list of pairs (where a pair is a list that holds two items). In fact it is so common that I decided to add a \keyword{syntactic sugar} for it. The following example shows how to use it:

\begin{REPL}
meruem> '((1 "one") (2 "two"))  ; this expression
((1 one) (2 two))
meruem> '{ 1 "one" 2 "two" }  ; is the same as this
((1 one) (2 two))
\end{REPL}

Notice the use of the quote operator. Without the quote, the first expression would have been written like \code{(list (list 1 "one") (list 2 "two"))}, which is quite verbose. But more importantly, notice how the two expressions in the example evaluate to the same values. That is because they are just the same expression, except that the second one used a syntactic sugar. In general, the form \code{((a b) (c d))} (where \code{a}, \code{b}, \code{c}, and \code{d} are all s-expressions) can also be written as \code{\{a b c d\}}.